plugins {
    id 'com.google.protobuf' version '0.9.4'
}

dependencies {
    api project(':mutr-core')

    // 비동기 통신을 위한 WebClient(WebFlux)
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // gRPC 관련 의존성
    // 1. gRPC 핵심 라이브러리 (MethodDescriptor 등이 포함됨)
    implementation 'net.devh:grpc-client-spring-boot-starter:2.15.0.RELEASE'
    implementation 'io.grpc:grpc-netty-shaded'
    implementation 'io.grpc:grpc-protobuf'
    implementation 'io.grpc:grpc-stub'
    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5'
}

def grpcDir = layout.buildDirectory.dir("generated/source/proto/main/grpc").get().asFile
def protoJavaDir = layout.buildDirectory.dir("generated/source/proto/main/java").get().asFile

tasks.withType(JavaCompile) {
    dependsOn 'generateProto'
}

tasks.register('copyProto', Copy) {
    from "../../proto"
    into "src/main/proto"
}

project.afterEvaluate {
    tasks.findByName('generateProto')?.dependsOn copyProto
    tasks.findByName('processResources')?.dependsOn copyProto
}

sourceSets {
    main {
        java {
            srcDirs += [ grpcDir ]
            srcDirs += [ protoJavaDir ]
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.24.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.58.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}